#
# Defines environment variables.
#
# Authors:
#   Jeff Putsch <jdputsch@gmail.com>
#
# Set key environment variables:
#    DOTZSH_VERSION, cdpath, infopath, manpath, path
#    DOTZSH, DOTZSH_CUSTOM
#
# Note: See README file for startup order on RHEL, OpenSuse Leap 15
#
# To accomodate the reset of the environmnet via the /etc/* files on
# login shells, we have two choices:
#   1. setup the environment in .zshenv, and use .zshrc for all configuration.
#   2. unset GLOBAL_RCS to prevent the loading of system init files entirely.
# 2018-08-18 I'm using opt. 1. If that proves too slow, I'll switch to opt. 2.
# 2018-10-22 opt 2 it is: this helps keep system profiles (e.g. bash) from messing
#                         us about.

# Don't read global configs
unsetopt GLOBAL_RCS

# Set the location of my zsh init files directory:
export DOTZSH=${${(%):-%N}:A:h} # and I though perl was baud-barf...
                                # ${(%):-%N} -> path to this script
                                # :A:h -> resolves symlinks, then gets parent
                                # See zshexpn(1), section HISTORY EXPANSION
export DOTZSH_VERSION=$(/usr/bin/head -1 "$DOTZSH/_version")

export DOTFILES=${DOTZSH:h}

# Save current PATH environment variable, incase we need it later
DOTZSH_ORIG_PATH=${PATH}

# Basic X11 environment settings
export XENVIRONMENT=${HOME}/.Xdefaults

# Set platform specific variables
PLAT=""
if [ -z "${DistroBasedOn}" ] && [ -f ${DOTFILES}/scripts/identify_platform.sh ]; then
    . ${DOTFILES}/scripts/identify_platform.sh
    PLAT=${DistroBasedOn}-${MAJOR_REV}-${MACH}
fi

# Setup XDG environment variables
export XDG_CONFIG_HOME="$HOME"/.config
export XDG_DATA_HOME="$HOME"/.local/share
export XDG_CACHE_HOME="$HOME"/.cache

# Setup Rust CARGO and RUSTUP locations
export CARGO_HOME="$XDG_DATA_HOME"/cargo
export RUSTUP_HOME="$XDG_DATA_HOME"/rustup

# Setup Go locations
export GOPATH="$XDG_DATA_HOME"/go

# Places we may need to set the SHELL environment variable
if [[ ${OS} = linux ]]; then
    if [[ "$(</proc/version)" = *Microsoft* ]]; then
        export SHELL=/usr/bin/zsh
    elif [[ -z "${SHELL}" ]]; then
        export SHELL=$(readlink /proc/$$/exe)
    fi
fi

## Do not use Apple specific shell sessions
SHELL_SESSIONS_DISABLE=1

# If modules is available, use it, else manage key paths here
if [ -f /usr/cadtools/bin/modules.dir/Shrc ]; then
    if [[ -o login ]]; then
      . /usr/cadtools/bin/modules.dir/Shrc
    fi
else
    # Paths - make sure these have unique entries, then set default vaules
    typeset -gU cdpath fpath mailpath manpath path
    typeset -gUT INFOPATH infopath
    
    # Set the the list of directories that cd searches.
    cdpath=(
      $cdpath
    )
    
    # Set the list of directories that `info` searches for manuals.
    infopath=(
      /usr/local/share/info
      /usr/share/info
      $infopath
    )
    
    # OSX has a path helper (/usr/libexec/path_helper) to set PATH and MANPATH
    # variables, normally I would use it, but I use APFs on Mac, therefore I don't
    # use path helper anymore: on APFS, the order of files read from /etc/paths.d
    # is not ascii/numeric anymore. therfore, we'll emulate it here.
    
    
    
    # Set the list of directories that Zsh searches for programs.
    if [[ $OSTYPE = darwin* ]] && [ -x /usr/libexec/path_helper ]; then
    	eval `/usr/libexec/path_helper -s`
    else
      # Set the list of directories that man searches for manuals.
      if [[ $OSTYPE != darwin* ]]; then
          manpath=(
            /usr/local/share/man
            /usr/share/man
            $manpath
          )
        for path_file in /etc/manpaths.d/*(.N); do
          manpath+=($(<$path_file))
        done
        unset path_file
      fi
      # Now set path entries
      path=(
        /usr/local/{bin,sbin}
        /usr/{bin,sbin}
        /{bin,sbin}
        ~/bin
        $path
      )
      for path_file in /etc/paths.d/*(.N); do
        path+=($(<$path_file))
      done
      unset path_file
    fi
    
    # Add personal, custom paths
    for path_file in ${HOME}/.paths.d/*(@N,.N); do
      [ -e $HOME/ZSH_DEBUG ] && echo ".zshenv: settings paths from $path_file" >> $HOME/ZSH_DEBUG
       for path_elem in $(cat ${path_file}); do
           if [ -d ${(e)path_elem} ]; then
               path+=(${(e)path_elem})
           fi
       done
       unset path_elem
    done
    unset path_file
    
    # Move some path elements to the beginning of our search path
    if [ -n "${PLAT}" ] && [ -d ${HOME}/${PLAT}/bin ]; then
        path=( ${HOME}/${PLAT}/bin $path)
    fi
    
    # Remove path elements that start with "/tmp/" -- a Maxim/Linux artifact:
    path=("${(@)path:#/tmp/*}")

    # MacOSX
    if [[ "$OSTYPE" = darwin*  && -d /opt/pkg ]]; then
      infopath=(
        $infopath
        /opt/pkg/info
      )
    fi
    
    # Linux
    if [[ "$OSTYPE" = linux* ]]; then
        [[ -d /usr/sepp/man ]] && manpath=($manpath /usr/sepp/man)
        [[ -d /home/tekcad/local/man ]] && manpath=($manpath /home/tekcad/local/man)
    fi
fi

# Language
if [[ -z "$LANG" ]]; then
  def_locale=$(locale -a | egrep 'en.*US.*UTF-8')
  if [[ -n "$def_locale" ]]; then
    export LANG=$def_locale
  else
    export LANG=C
  fi
fi

## Set P4 environment variables based on the platform & host
if [[ ${OS} = "darwin" ]] && [[ $HOST = JPUTSCH-M01* ]]; then
    export P4CONFIG=.p4config
    export P4PORT=p4e.adsiv.analog.com:1666
    export P4USER=jputsch

    if [ -x /Applications/p4merge.app/Contents/MacOS/p4merge ]; then
        p4merge() {
            /Applications/p4merge.app/Contents/MacOS/p4merge "$@"
        }
    fi
fi

## PIP behavior, for python
# require virtualenv for using pip
export PIP_REQUIRE_VIRTUALENV=true

# # Browser (Default)
# if [[ "$OSTYPE" == darwin* ]]; then
#   export BROWSER='open'
# fi

# local Variables:
# mode: sh
# eval: (sh-set-shell "zsh")
# End:
